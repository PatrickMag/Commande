' Gambas class file

Private $bs As Barselection
Private $rescha As Result
Private $gchau As Gesteven
Private $che As New CheckBox[]

Public Enum dimanche, lundi, mardi, mercredi, jeudi, vendredi, samedi
Public jours As String[] = ["Dimanche", "Lundi", "Mardi", "Mercredi", "Jeudi", "Vendredi", "Samedi"]


Public Sub _new()

  Dim che As CheckBox
  Dim i As Integer
  
  $bs = New Barselection("111111110", Panel1) As "bar"
  For i = 0 To 6
    che = New CheckBox(jtr)
    che.Text = jours[i]
    che.Expand = True
    $che.Add(che)
  Next
  affprem
  
End

Private Sub affprem()

  $rescha = connect.ccon.Exec("Select * FROM chauffeur")
  If $rescha.Available Then
    $rescha.MoveFirst
    affiche
  Endif
  
End

Private Sub affiche()
  
  Dim geven As Gesteven
  Dim i As Integer
  
  Textnum.Text = $rescha!idchauffeur
  Textnom.Text = $rescha!nom
  Textpren.Text = $rescha!prenom
  Dateemb.Value = $rescha!dtemb
  Datenais.Value = $rescha!dtnais
  secu.Text = $rescha!nss
  panier.Value = CBool($rescha!panier)
  rtt.Value = CBool($rescha!rtt)
  For i = 0 To 6
    $che[i].Value = CBool($rescha[jours[i]])
  Next
  If Not $gchau Then
    $gchau = New Gesteven(GridView1, Textnum.Text, "C")
  Else
    $gchau.Identifiant = Textnum.Text
  Endif
  
End

Private Sub valide()
  
  Dim i As Integer
  
  $rescha!idchauffeur = Textnum.Text
  $rescha!nom = Textnom.Text
  $rescha!prenom = Textpren.Text
  $rescha!dtemb = Dateemb.Value
  $rescha!dtnais = Datenais.Value
  $rescha!nss = secu.Text
  $rescha!panier = panier.Value
  $rescha!rtt = rtt.Value
  For i = 0 To 6
    $rescha[jours[i]] = $che[i].Value
  Next
  
  $rescha.Update
  connect.ccon.Commit
  affprem
  
End

Private Sub mablanc()
  
  Textnum.Clear
  Textnom.Clear
  Textpren.Clear
  secu.Clear
  
  
End

Public Sub bar_precedant()

  If Not $rescha.MovePrevious() Then affiche

End

Public Sub bar_suivant()

  If Not $rescha.MoveNext() Then affiche

End

Public Sub bar_premier()

  If Not $rescha.MoveFirst() Then affiche

End

Public Sub bar_dernier()

  If $rescha.Available Then
    If Not $rescha.MoveLast() Then affiche
  Endif
  
End

Public Sub bar_creer()

  $rescha = connect.ccon.Create("chauffeur")
  mablanc
  Textnum.ReadOnly = False
  Textnum.Select

End

Public Sub bar_suprimer()

  If $rescha.Available Then
    connect.ccon.Delete("chauffeur", "idchauffeur=&1", $rescha!idchauffeur)
    affprem
  Endif
  
End

Public Sub bar_modif()

  If $rescha.Available Then
    $rescha = connect.ccon.Edit("chauffeur", "idchauffeur=&1 ", $rescha!idchauffeur)
 Else
   Stop Event
  Endif

End

Public Sub bar_quitter()

  Me.Close

End

Public Sub bar_valide(creer As Boolean, modif As Boolean)

  If creer Or modif Then valide

End

Public Sub bar_annule(creer As Boolean, annul As Boolean)

  connect.ccon.Rollback
  affprem

End
