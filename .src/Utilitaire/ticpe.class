' Gambas class file

Create Static

Public Enum GnrAgri, Carburant                ''concerne les taxes sur le carburant ou le gnr
Public Enum Adlivraison, Adcli                ''Quelle adresse est passée livraison ou facturation
Public categorie As String[] = ["GNR détaxé", "Carburant"]

Private $adr As Integer                       ''quelle adresse 
Private $rcli As Result                       ''fiche client/adresse livraison

Property Read date As LDate Use $dtval          ''date de validité pour le gnr
Property Read NumAgri As String Use $numa       ''n° de dossier pour le gnr
Property Read NumCarb As String Use $numc       ''n° que l'on a attribué pour le carburant
Property Read region As String Use $regi        ''code region pour le carburant

Public Sub _new(client As String, nadr As String)

  Dim rcli1 As Result
  
  If Val(nadr) < 0 Then
    $rcli = connect.cconlaurux.Exec("SELECT * FROM Fiches_Cli WHERE cli_code=&1", client)
    If Not $rcli.Available Then Error.Raise("Client innexistant")
    $adr = Adcli
    $regi = $rcli!cli_tipp
    If $rcli!dtagri Then $dtval = New LDate($rcli!dtagri)
    $numa = $rcli!numagri
    $numc = $rcli!numtipp
  Else
    $rcli = connect.cconlaurux.Exec("SELECT * FROM Fiches_AdrlivC WHERE num=&1", Val(nadr))
    $adr = Adlivraison
    rcli1 = connect.cconlaurux.Exec("SELECT * FROM Fiches_Cli WHERE cli_code=&1", client)
    If rcli1!cli_tipp Then $regi = rcli1!cli_tipp Else $regi = $rcli!tipp
    If rcli1!numagri Then $numa = rcli1!numagri Else $numa = $rcli!numagri
    If rcli1!numtipp Then $numc = rcli1!numtipp Else $numc = $rcli!numtipp
    If rcli1!dtagri Then 
      $dtval = New LDate(rcli1!dtagri) 
    Else If $rcli!dtagri Then
      $dtval = New LDate($rcli!dtagri)
    Endif
  Endif
  

End

Public Sub _call(client As Variant, nadr As String) As Ticpe

  Dim t As Ticpe
  
  t = New Ticpe(client, nadr)
  Return t
  
End

Public Sub Valide(art As String) As Boolean

  Dim res As Result
  
  res = connect.cconlaurux.Exec("SELECT * FROM Art_Tipp WHERE code=&1", art)
  If res.Available Then
    If res!ticpe = GnrAgri Then
      If $numa Then
        If $dtval.Avant(LDate(Now), True) Then 
          Message.Error("Attestation périmée de puis le : " & $dtval.L & "\nSaisie impossible")
          Return True
        Endif
      Else
        Message.Error(Adliv(res!ticpe) & "Le numéro de dossier n'existe pas\nSaisie impossible")
        Return True
      Endif
    Else If res!ticpe = Carburant Then
      If Not $regi Then
        Message.Error(Adliv(res!ticpe) & "Pas de région déclarée pour ce client\nSaisie impossible")
        Return False
      Endif
      If Not $numc Then
        Message.Info(Adliv(res!ticpe) & "Vous devriez renseigner un n° de dossier")
      Endif
    Endif
  Endif
  Return False

End

Private Sub Adliv(cat As Integer) As String
  
  If $adr = Adcli Then Return "Adresse de facturation\n" Else Return "Adresse de livraison\n"
  
End
